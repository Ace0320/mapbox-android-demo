apply plugin: 'com.android.application'
apply from: "$project.rootDir/gradle/script-git-version.gradle"
apply plugin: 'com.google.firebase.firebase-perf'

android {
    compileSdkVersion androidVersions.compileSdkVersion
    buildToolsVersion androidVersions.buildToolsVersion

    defaultConfig {
        applicationId "com.mapbox.mapboxandroiddemo"

        minSdkVersion androidVersions.minWearSdkVersion
        targetSdkVersion androidVersions.targetSdkVersion

        versionCode gitVersionCode
        versionName gitVersionName
    }
    signingConfigs {
        release {
            keyAlias System.getenv("BITRISEIO_ANDROID_KEYSTORE_ALIAS")
            keyPassword System.getenv("BITRISEIO_ANDROID_KEYSTORE_PRIVATE_KEY_PASSWORD")
            storeFile file("$project.rootDir/mapbox_demo.keystore")
            storePassword System.getenv("BITRISEIO_ANDROID_KEYSTORE_PASSWORD")
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    lintOptions {
        abortOnError false
    }
}


repositories {
    maven {
        url "https://mapbox.bintray.com/mapbox_private"
        credentials {
            username project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
            password project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
        }
    }
}



dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    compileOnly wearDependencies.googleWearable
    implementation wearDependencies.supportGoogleWearable

    implementation wearDependencies.gpsWearable
    implementation wearDependencies.gpsLocation

    // Mapbox dependencies
    api (dependenciesList.mapboxMapSdk)

    // Mapbox Android internal libraries
    implementation dependenciesList.mapboxAuth
    implementation dependenciesList.mapboxAnalytics

    // Firebase
    implementation dependenciesList.firebaseCrash
    implementation dependenciesList.firebasePerf

    implementation project(':SharedCode')
}

apply from: "$project.rootDir/gradle/checkstyle.gradle"
